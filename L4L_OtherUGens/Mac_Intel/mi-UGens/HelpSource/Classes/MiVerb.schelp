TITLE:: MiVerb
summary:: stereo reverb
categories:: UGens>Reverbs
related:: Classes/FreeVerb, Classes/MiPlaits, Classes/MiRings

DESCRIPTION::
Stereo reverb unit, based on Jon Dattorro's paper about the Griesinger reverb topology

note::
This was ported from the mutable instruments eurorack modules reverb units - link::https://mutable-instruments.net/::. Original code by Ã‰milie Gillet. ::

CLASSMETHODS::

METHOD:: ar

ARGUMENT:: inputArray
Input UGens (channelArray). MultiChannel inputs will be placed left and right in the dry stereo image.


ARGUMENT:: time
Reverb time (0. -- 1.25). At values of 1. or greater the reverb tail doesn't decay anymore, but keeps growing which drives the output into soft distortion. Can be nice, but watch out for DCs building up.

ARGUMENT:: drywet
Dry/wet reverb balance (0. -- 1.)

ARGUMENT:: damp
Amount of high frequency damping (0. -- 1.)

ARGUMENT:: hp
Amount of high pass filtering for DC removal and more (0. -- 1.)

ARGUMENT:: freeze
Freezes the reverb tail and closes the input. A non-zero value activates 'freeze'.

ARGUMENT:: diff
Reverb diffusion (0. -- 1.), original value in Dattorro's paper is 0.625;

EXAMPLES::

code::

  // basic

{ MiVerb.ar(Impulse.ar(1)) }.play


(  // stereo example, with freezing
{
	var imp = Dust.ar([0.7, 0.8]);
    var freq = Latch.ar(PinkNoise.ar(24,80), imp).midicps;
    var input = RLPF.ar(imp, freq, 0.002, 3);

	var freez = TRand.kr(-1, 1, Dust.kr(0.7));
    var revtime = 0.8;
    var drywet = 0.5;
    var damping = 0.3;

	var diff = LFNoise1.kr(0.1,0.5,0.5);

    MiVerb.ar(input, revtime, drywet, damping, freeze: freez);
}.play
)


(  // playing with the fire...
{
	var freq = LFNoise0.kr(0.3).range(400, 2500);
	var input = RLPF.ar(Impulse.ar(0.3), freq, 0.1);
    var time = LFNoise2.kr(0.3, 0.1, 1.03);   // modulate rev time above and below 1.0!
	var damp = LFNoise2.kr(0.2).range(0, 0.7);
    MiVerb.ar(input, time, 0.9, damp, 0.1, mul: 0.5);
}.play
)


::